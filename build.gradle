/*
 * Copyright 2018 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

group = 'com.example.notification.template'
version = '0.1-SNAPSHOT'

// these values that go into plugin.xml
project.ext.pluginDesc = [
        id         : 'com.newchiwan.devops',
        version    : project.version,
        goCdVersion: '21.4.0',
        name       : 'DingTalk Notifier',
        description: 'DingTalk Notifier',
        vendorName : 'NewChiwan',
        vendorUrl  : 'https://github.com/user/repository'
]

sourceCompatibility = 1.8
targetCompatibility = 1.8

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    jarImpl.extendsFrom(implementation)
}

dependencies {
    implementation group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    implementation group: 'com.google.guava', name: 'guava', version: '23.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.3'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testImplementation group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            expand project.pluginDesc
        }

        filesMatching('plugin.properties') {
            expand project.pluginDesc
        }
    }
}

jar {
    from(configurations.jarImpl) {
        into "lib/"
    }
}
